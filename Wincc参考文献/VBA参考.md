## 3.6 VBA 参考

### 3.6.1 图形编辑器的对象模型

#### 3.6.1.1 VBA 参考

**VBA 对象模型**

当您点击一个对象名称时，您将看到一个详细的描述。

> **注意**
>
> 在接下来的描述中，前缀 "HMI" 将被省略。请注意，在代码中您必须为对象添加 "HMI" 前缀，例如 "HMISymbolLibrary"。



**另请参见**
*   Events 对象 (列表) (Page 2050)
*   SymbolLibraries 对象 (列表) (Page 2160)
*   Actions 对象 (列表) (Page 1982)
*   Application 对象 (Page 1993)
*   DataLanguage 对象 (Page 2024)
*   DataLanguages 对象 (列表) (Page 2025)
*   Document 对象 (Page 2032)
*   Documents 对象 (列表) (Page 2035)
*   Event 对象 (Page 2049)
*   HMIDefaultObjects 对象 (列表) (Page 2067)
*   HMIObject 对象 (Page 2070)
*   HMIObjects 对象 (列表) (Page 2072)
*   FolderItem 对象 (Page 2055)
*   FolderItems 对象 (列表) (Page 2056)
*   VBA 参考: ActionDynamic (Page 1832)
*   VBA 参考: HMIObjects (Page 1834)
*   VBA 参考: Languages (Page 1836)
*   Layer 对象 (Page 2083)
*   Layers 对象 (列表) (Page 2084)
*   Menu 对象 (Page 2091)
*   Menus 对象 (列表) (Page 2093)
*   MenuItem 对象 (Page 2095)
*   MenuItems 对象 (列表) (Page 2097)
*   Properties 对象 (列表) (Page 2123)
*   Toolbar 对象 (Page 2167)
*   Toolbars 对象 (列表) (Page 2168)
*   ToolbarItem 对象 (Page 2170)
*   ToolbarItems 对象 (列表) (Page 2172)
*   View 对象 (Page 2189)
*   Views 对象 (列表) (Page 2190)
*   SelectedObjects 对象 (列表) (Page 2141)
*   SymbolLibrary 对象 (Page 2162)
*   Property 对象 (Page 2124)

---

#### 3.6.1.2 VBA 参考: ActionDynamic

**VBA 对象模型: ActionDynamic**

"ActionDynamic" 代表了动态和动作的接口端口，例如脚本、动态对话框、直接连接和触发器。

当您点击一个对象名称时，您将看到一个详细的描述。



**另请参见**
*   VBA 参考 (Page 1830)
*   AnalogResultInfo 对象 (Page 1991)
*   AnalogResultInfos 对象 (列表) (Page 1992)
*   BinaryResultInfo 对象 (Page 2002)
*   BitResultInfo 对象 (Page 2003)
*   Actions 对象 (列表) (Page 1982)
*   DestLink 对象 (Page 2028)
*   DirectConnection 对象 (Page 2030)
*   DynamicDialog 对象 (Page 2038)
*   QualityCodeStateValue 对象 (Page 2126)
*   QualityCodeStateValues 对象 (列表) (Page 2128)
*   ScriptInfo 对象 (Page 2140)
*   SourceLink 对象 (Page 2147)
*   Trigger 对象 (Page 2174)
*   VariableStateValue 对象 (Page 2183)
*   VariableStateValues 对象 (列表) (Page 2184)
*   VariableTrigger 对象 (Page 2186)
*   VariableTriggers 对象 (列表) (Page 2188)
*   ActionType 属性 (Page 2194)
*   DynamicStateType 属性 (Page 2303)

---

#### 3.6.1.3 VBA 参考: HMIObjects

**VBA 对象模型: HMIObjects**

当您点击一个对象名称时，您将看到一个详细的描述。



| 图例 | 描述 |
| :--- | :--- |
| **对象和列表** | |
| **对象** | |
|  | 提供详细视图。 |
|  | 可以进行多语言对象组态。 |
| * | 不在 DefaultObjects 列表中。 |

**另请参见**
*   VBA 参考 (Page 1830)
*   PolyLine 对象 (Page 2120)
*   GroupDisplay 对象 (Page 2063)
*   3DBarGraph 对象 (Page 1978)
*   ActiveXControl 对象 (Page 1984)
*   ApplicationWindow 对象 (Page 1995)
*   Button 对象 (Page 2004)
*   CheckBox 对象 (Page 2008)
*   Circle 对象 (Page 2011)
*   CircularArc 对象 (Page 2014)
*   Line 对象 (Page 2086)
*   OLEObject 对象 (Page 2105)
*   OptionGroup 对象 (Page 2107)
*   PictureWindow 对象 (Page 2111)
*   PieSegment 对象 (Page 2114)
*   Polygon 对象 (Page 2117)
*   Property 对象 (Page 2124)
*   Rectangle 对象 (Page 2131)
*   RoundButton 对象 (Page 2133)
*   RoundRectangle 对象 (Page 2137)
*   Slider 对象 (Page 2144)
*   StaticText 对象 (Page 2149)
*   StatusDisplay 对象 (Page 2152)
*   TextList 对象 (Page 2163)
*   Ellipse 对象 (Page 2040)
*   EllipseArc 对象 (Page 2043)
*   EllipseSegment 对象 (Page 2046)
*   GraphicObject 对象 (Page 2058)
*   Group 对象 (Page 2061)
*   HMIDefaultObjects 对象 (列表) (Page 2067)
*   HMIObject 对象 (Page 2070)
*   HMIObjects 对象 (列表) (Page 2072)
*   IOField 对象 (Page 2074)
*   BarGraph 对象 (Page 1998)
*   GroupedObjects 对象 (列表) (Page 2066)
*   VBA 参考: Languages (Page 1836)
*   SelectedObjects 对象 (列表) (Page 2141)
*   CustomizedObject 对象 (Page 2021)
*   FaceplateObject 对象 (Page 2052)
*   AdvancedAnalogDisplay 对象 (Page 1986)
*   AdvancedStateDisplay 对象 (Page 1989)

---

#### 3.6.1.4 VBA 参考: Languages

**VBA 对象模型: Languages**

当您点击一个对象名称时，您将看到一个详细的描述。



**另请参见**
*   VBA 参考 (Page 1830)
*   LanguageFont 对象 (Page 2078)
*   LanguageFonts 对象 (列表) (Page 2079)
*   LanguageText 对象 (Page 2081)
*   LanguageTexts 对象 (列表) (Page 2082)

---

#### 3.6.1.5 事件

**A-D**

##### Activated 事件

**描述**
当一个画面在图形编辑器中被激活时发生。例如，当您在两个画面之间切换时会发生这种情况。

**语法**
`Document_Activated(CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当画面被激活时会输出一条消息：

```vb
Private Sub Document_Activated(CancelForwarding As Boolean)
'VBA76
MsgBox "文档获得了焦点。" & vbCrLf &_
"此事件 (Document_Activated) 由文档本身引发"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   事件处理 (Page 1795)

##### BeforeClose 事件

**描述**
在画面关闭之前立即发生。

**语法**
`Document_BeforeClose(Cancel As Boolean, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，在画面关闭前会输出一条消息：

```vb
Private Sub Document_BeforeClose(Cancel As Boolean, CancelForwarding As Boolean)
'VBA77
MsgBox "事件 Document_BeforeClose 被引发"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeDocumentClose 事件

**描述**
在画面关闭之前立即发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_BeforeDocumentClose(Document As HMIDocument, Cancel As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 即将被关闭的画面。 |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，在画面关闭前会输出一条消息：

```vb
Private Sub objGDApplication_BeforeDocumentClose(ByVal Document As IHMIDocument, Cancel As Boolean)
'VBA78
MsgBox "文档 " & Document.Name & " 将在点击确定后关闭"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeDocumentSave 事件

**描述**
在画面保存之前立即发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_BeforeDocumentSave(Document As HMIDocument, Cancel As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 即将被保存的画面。 |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，在画面保存前会输出一条消息：

```vb
Private Sub objGDApplication_BeforeDocumentSave(ByVal Document As IHMIDocument, Cancel As Boolean)
'VBA79
MsgBox Document.Name & "-保存将在点击确定后开始。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeHMIObjectDelete 事件

**描述**
在画面中的一个对象被删除之前立即发生。

**语法**
`BeforeHMIObjectDelete(ByVal HMIObject As IHMIObject, Cancel As Boolean, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIObject` (IHMIObject) | 标识要被删除的对象。 |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，会输出一条消息，标识要被删除的对象：

```vb
Private Sub Document_BeforeHMIObjectDelete(ByVal HMIObject As IHMIObject, Cancel As Boolean, CancelForwarding As Boolean)
'VBA80
Dim strObjName As String
Dim strAnswer As String
' '"strObjName" 包含了被删除对象的名称
strObjName = HMIObject.ObjectName
strAnswer = MsgBox("您确定要删除 " & strObjName & "?", vbYesNo)
If strAnswer = vbNo Then
'如果按下 "No" -> 设置 Cancel 为 true 以阻止删除
Cancel = True
End If
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeLibraryFolderDelete 事件

**描述**
在组件库中的一个文件夹被删除之前立即发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_BeforeLibraryFolderDelete(LibObject As HMIFolderItem, Cancel As Boolean)`

**参数 (可选)**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 即将被删除的文件夹。 |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```在以下示例中，在组件库中的一个文件夹被删除前会输出一条消息：

```vb
Private Sub objGDApplication_BeforeLibraryFolderDelete(ByVal LibObject As HMIFolderItem, Cancel As Boolean)
'VBA81
MsgBox "库文件夹 " & LibObject.Name & " 即将被删除..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeLibraryObjectDelete 事件

**描述**
在组件库中的一个对象被删除之前立即发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_BeforeLibraryObjectDelete(LibObject As HMIFolderItem, Cancel As Boolean)`

**参数 (可选)**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 即将被删除的对象。 |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，在组件库中的一个对象被删除前会输出一条消息：

```vb
Private Sub objGDApplication_BeforeLibraryObjectDelete(ByVal LibObject As HMIFolderItem, Cancel As Boolean)
'VBA82
MsgBox "对象 " & LibObject.Name & " 即将被删除..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeQuit 事件

**描述**
在图形编辑器关闭之前立即发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_BeforeQuit(Cancel As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在此示例中，在图形编辑器关闭前不久会输出一条消息。

```vb
Private Sub objGDApplication_BeforeQuit(Cancel As Boolean)
'VBA83
MsgBox "图形编辑器即将关闭"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeSave 事件

**描述**
在画面保存之前立即发生。

**语法**
`Document_BeforeSave(Cancel As Boolean, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，在画面保存前会输出一条消息：

```vb
Private Sub Document_BeforeSave(Cancel As Boolean, CancelForwarding As Boolean)
'VBA84
MsgBox "文档即将保存..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### BeforeVisibleFalse 事件

**描述**
在图形编辑器应用程序从可见设置为不可见之前立即发生。

**语法**
`Document_BeforeVisibleFalse(Cancel As Boolean, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Cancel` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
--

**另请参见**
*   VBA 参考 (Page 1830)

##### ConnectionEvent 事件

**描述**
当两个对象通过连接器连接时发生。

**语法**
`ConnectionEvent(eConnEventType, HMIConnector, HMIConnectedObject, CancelProcess, CancelForwarding)`

**参数 (可选)**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `eConnEventType` (HMIConnectionEventType) | -- |
| `HMIConnector` (HMIObject) | -- |
| `HMIConnectedObject` (HMIObject) | -- |
| `CancelProcess` (Boolean) | 如果要取消命令处理，则为 `TRUE`。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
--

**另请参见**
*   VBA 参考 (Page 1830)

##### DataLanguageChanged 事件

**描述**
当项目语言已更改时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DataLanguageChanged(lCID As Long)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `lCID` (Long) | 项目语言标识符。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出新设置的项目语言：

```vb
Private Sub objGDApplication_DataLanguageChanged(ByVal lCID As Long)
'VBA87
MsgBox "数据语言已更改为 " & Application.CurrentDataLanguage & "."
End Sub
```

**另请参见**
*   使用 VBA 进行与语言相关的组态 (Page 1706)
*   VBA 参考 (Page 1830)

##### DesktopLanguageChanged 事件

**描述**
当用户界面语言已更改时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DesktopLanguageChanged(lCID As Long)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `lCID` (Long) | 用户界面语言标识符。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出新设置的桌面语言：

```vb
Private Sub objGDApplication_DesktopLanguageChanged(ByVal lCID As Long)
'VBA88
MsgBox "桌面语言已更改为 " & Application.CurrentDesktopLanguage & "."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   使用 VBA 进行与语言相关的组态 (Page 1706)

##### DocumentActivated 事件

**描述**
当一个画面在图形编辑器中被激活时发生。例如，当您在两个画面之间切换时会发生这种情况。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DocumentActivated(Document As HMIDocument)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 即将被激活的画面。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出一条消息，标识已激活的画面：

```vb
Private Sub objGDApplication_DocumentActivated(ByVal Document As IHMIDocument)
'VBA89
MsgBox "文档 " & Document.Name & " 获得了焦点。" & vbCrLf &_
"此事件由应用程序引发。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### DocumentCreated 事件

**描述**
当在图形编辑器中创建了一个新画面时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DocumentCreated(Document As HMIDocument)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 已被创建的画面。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出新创建的画面的名称：

```vb
Private Sub objGDApplication_DocumentCreated(ByVal Document As IHMIDocument)
'VBA90
MsgBox Document.Name & " 即将被创建。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### DocumentOpened 事件

**描述**
当一个画面已被打开时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DocumentOpened(Document As HMIDocument)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 已被打开的画面。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出一条消息，标识已打开的画面：

```vb
Private Sub objGDApplication_DocumentOpened(ByVal Document As IHMIDocument)
'VBA91
MsgBox Document.Name & " 已打开。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### DocumentSaved 事件

**描述**
当一个画面已在图形编辑器中保存时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_DocumentSaved(Document As HMIDocument)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Document` (HMIDocument) | 已被保存的画面。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出一条消息，标识已保存的画面：

```vb
Private Sub objGDApplication_DocumentSaved(ByVal Document As IHMIDocument)
'VBA92
MsgBox Document.Name & " 已保存。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### DocumentPropertyChanged 事件

**描述**
当画面属性更改时发生。

**语法**
`Document_DocumentPropertyChanged(ByVal Property As IHMIProperty, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Property` (IHMIProperty) | 标识已更改的属性。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，会输出一条消息，标识正在更改的画面属性：

```vb
Private Sub Document_DocumentPropertyChanged(ByVal Property As IHMIProperty, CancelForwarding As Boolean)
'VBA93
Dim strPropName As String
'"strPropName" 包含了被修改属性的名称
strPropName = Property.Name
MsgBox "画面属性 " & strPropName & " 已被修改..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

**F-Z**

##### HMIObjectAdded 事件

**描述**
当添加一个对象时发生。

**语法**
`Document_HMIObjectAdded(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIObject` (IHMIObject) | 标识正在添加的对象。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，会输出一条消息，标识已添加的对象：

```vb
Private Sub Document_HMIObjectAdded(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)
'VBA94
Dim strObjName As String
' '"strObjName" 包含了被添加对象的名称
strObjName = HMIObject.ObjectName
MsgBox "对象 " & strObjName & " 已被添加..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### HMIObjectMoved 事件

**描述**
当一个对象被移动时发生。

**语法**
`Document_HMIObjectMoved(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIObject` (IHMIObject) | 标识正在移动的对象。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，会输出一条消息，标识已移动的对象：

```vb
Private Sub Document_HMIObjectMoved(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)
'VBA95
Dim strObjName As String
' '"strObjName" 包含了被移动对象的名称
strObjName = HMIObject.ObjectName
MsgBox "对象 " & strObjName & " 已被移动..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### HMIObjectPropertyChanged 事件

**描述**
当一个对象属性被更改时发生。

**语法**
`Document_HMIObjectPropertyChanged(ByVal Property As IHMIProperty, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Property` (IHMIProperty) | 标识已更改的属性。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，会输出一条消息，标识已更改的对象属性：

```vb
Private Sub Document_HMIObjectPropertyChanged(ByVal Property As IHMIProperty, CancelForwarding As Boolean)
'VBA96
Dim strObjProp As String
Dim strObjName As String
Dim varPropValue As Variant
'
'"strObjProp" 包含了被修改属性的名称
'"varPropValue" 包含了新值
strObjProp = Property.Name
varPropValue = Property.value
'
'"strObjName" 包含了被选中对象的名称，其属性被修改
strObjName = Property.Application.ActiveDocument.Selection(1).ObjectName
MsgBox "对象 " & strObjName & " 的属性 " & strObjProp & " 已被修改... " & vbCrLf & "新值为: " & varPropValue
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### HMIObjectResized 事件

**描述**
当一个对象的大小被更改时发生。

**语法**
`Document_HMIObjectResized(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIObject` (IHMIObject) | 标识正在调整大小的对象。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当一个对象的大小已更改时，会输出一条消息：

```vb
Private Sub Document_HMIObjectResized(ByVal HMIObject As IHMIObject, CancelForwarding As Boolean)
'VBA97
Dim strObjName As String
' '"strObjName" 包含了被修改对象的名称
strObjName = HMIObject.ObjectName
MsgBox "" & strObjName & " 的大小已被修改..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### LibraryFolderRenamed 事件

**描述**
当组件库中的一个文件夹已被重命名时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_LibraryFolderRenamed(LibObject As HMIFolderItem, OldName As String)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 已重命名的文件夹。 |
| `OldName` (String) | 已重命名文件夹的原始名称。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出旧的和新的文件夹名称：

```vb
Private Sub objGDApplication_LibraryFolderRenamed(ByVal LibObject As HMIFolderItem, ByVal OldName As String)
'VBA98
MsgBox "库文件夹 " & OldName & " 已重命名为: " & LibObject.DisplayName
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### LibraryObjectRenamed 事件

**描述**
当组件库中的一个对象已被重命名时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_LibraryObjectRenamed(LibObject As HMIFolderItem, OldName As String)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 已重命名的对象。 |
| `OldName` (String) | 已重命名对象的原始名称。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出旧的和新的对象名称：

```vb
Private Sub objGDApplication_LibraryObjectRenamed(ByVal LibObject As IHMIFolderItem, ByVal OldName As String)
'VBA99
MsgBox "对象 " & OldName & " 已重命名为: " & LibObject.DisplayName
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### LibraryObjectAdded 事件

**描述**
当一个对象已被添加到组件库时发生。

**语法**
`HMIObjectPropertyChanged(ByVal Property As IHMIProperty, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (IHMIFolderItem) | 标识库对象。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当一个对象已被添加到组件库时，会输出一条消息：

```vb
Private Sub Document_LibraryObjectAdded(ByVal LibObject As IHMIFolderItem, CancelForwarding As Boolean)
'VBA100
Dim strObjName As String
' '"strObjName" 包含了被添加对象的名称
strObjName = LibObject.DisplayName
MsgBox "对象 " & strObjName & " 已被添加到画面中。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### MenuItemClicked 事件

**描述**
当用户定义的菜单中的一个条目被点击时发生。

> **注意**
>
> 此事件既是应用程序级的也是文档级的。
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`

**语法**
`<Name>_MenuItemClicked(ByVal MenuItem As IHMIMenuItem)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `MenuItem` (IHMIMenuItem) | 标识用户定义的菜单。 |

**示例**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'首先用 "F5" 执行此过程
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，当用户定义的菜单中的第一个条目被点击时，会输出一条消息：

```vb
Private Sub objGDApplication_MenuItemClicked(ByVal MenuItem As IHMIMenuItem)
'VBA101
Dim objMenuItem As HMIMenuItem
Dim varMenuItemKey As Variant
Set objMenuItem = MenuItem
'
'"objMenuItem" 包含了被点击的菜单项
'"varMenuItemKey" 包含了被点击的用户定义菜单项的 "Key" 参数的值
varMenuItemKey = objMenuItem.Key
Select Case MenuItem.Key
Case "mItem1_1"
MsgBox "第一个菜单项被点击了。"
End Select
End Sub
```

**另请参见**
*   如何为菜单和工具栏分配 VBA 宏 (Page 1723)
*   VBA 参考 (Page 1830)

##### NewLibraryFolder 事件

**描述**
当组件库中已创建一个文件夹时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_NewLibraryFolder(LibObject As HMIFolderItem)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 新创建的文件夹。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出新文件夹的名称：

```vb
Private Sub objGDApplication_NewLibraryFolder(ByVal LibObject As HMIFolderItem)
'VBA102
MsgBox "库文件夹 " & LibObject.DisplayName & " 已被添加。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### NewLibraryObject 事件

**描述**
当组件库中已创建一个对象时发生。

**语法**
> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

`objGDApplication_NewLibraryObject(LibObject As HMIFolderItem)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `LibObject` (HMIFolderItem) | 新创建的对象。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，会输出新对象的名称：

```vb
Private Sub objGDApplication_NewLibraryObject(ByVal LibObject As IHMIFolderItem)
'VBA103
MsgBox "对象 " & LibObject.DisplayName & " 已被添加。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### Opened 事件

**描述**
当一个画面被打开时发生。

**语法**
`Document_Opened(CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当画面被打开时会输出一条消息：

```vb
Private Sub Document_Opened(CancelForwarding As Boolean)
'VBA104
MsgBox "文档现在已打开..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### Saved 事件

**描述**
在一个画面被保存后发生。

**语法**
`Document_Saved(CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当画面已被保存时会输出一条消息：

```vb
Private Sub Document_Saved(CancelForwarding As Boolean)
'VBA105
MsgBox "文档已保存..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### SelectionChanged 事件

**描述**
当选择已更改时发生。

**语法**
`Document_SelectionChanged(CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
在以下示例中，当一个新对象被选中时会输出一条消息：

```vb
Private Sub Document_SelectionChanged(CancelForwarding As Boolean)
'VBA106
MsgBox "选择已更改..."
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### Started 事件

**描述**
当图形编辑器已启动时发生。

**语法**
`objGDApplication_Started()`

> **注意**
>
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`
> 在下面的示例中，名称 "objGDApplication" 被用来替代 `<Name>`。

**参数**
--

**示例**
声明应用程序。
`Dim WithEvents objGDApplication As grafexe.Application`
设置事件标签。

```vb
Private Sub Document_Opened(CancelForwarding As Boolean)
 Set objGDApplication = Me.Application
End Sub
```
查询 "Started" 事件并输出消息。

```vb
Private Sub objGDApplication_Started()
'VBA107
'此事件在 objGDApplication_Started() 之前引发
 MsgBox "图形编辑器已启动！"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### ToolbarItemClicked 事件

**描述**
当用户定义的工具栏中的一个图标被点击时发生。

> **注意**
>
> 此事件既是应用程序级的也是文档级的。
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`

**语法**
`Document_ToolbarItemClicked(ByVal ToolbarItem As IHMIToolbarItem)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ToolbarItem` (IHMIToolbarItem) | 标识该符号。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，当第一个用户定义的图标被点击时，会输出一条消息：

```vb
Private Sub Document_ToolbarItemClicked(ByVal ToolbarItem As IHMIToolbarItem)
'VBA108
Dim objToolbarItem As HMIToolbarItem
Dim varToolbarItemKey As Variant
Set objToolbarItem = ToolbarItem
'
'"varToolbarItemKey" 包含了被点击的用户定义工具栏项的 "Key" 参数的值
varToolbarItemKey = objToolbarItem.Key
'
Select Case varToolbarItemKey
Case "tItem1_1"
MsgBox "第一个工具栏图标被点击了！"
End Select
End Sub
```

**另请参见**
*   如何为菜单和工具栏分配 VBA 宏 (Page 1723)
*   VBA 参考 (Page 1830)

##### ViewCreated 事件

**描述**
当一个画面的副本已创建时发生。

> **注意**
>
> 此事件既是应用程序级的也是文档级的。
> 为确保应用程序级事件在项目中可用，必须让图形编辑器知道该应用程序。这通过以下语句完成：
> `Dim WithEvents <Name> As grafexe.Application`

**语法**
`Document_ViewCreated(ByVal pView As IHMIView, CancelForwarding As Boolean)`

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `pView` (IHMIView) | 标识画面的副本。 |
| `CancelForwarding` (Boolean) | 如果事件不打算被转发，则为 `TRUE`。默认设置为 `False`。 |

**示例:**
执行以下过程以使下面的示例正常工作：

```vb
Private Sub SetApplication()
'此过程必须首先用 "F5" 执行
Set objGDApplication = grafexe.Application
End Sub
```
在以下示例中，当创建了画面的新副本时，会输出副本画面的数量。

```vb
Private Sub Document_ViewCreated(ByVal pView As IHMIView, CancelForwarding As Boolean)
'VBA109
Dim iViewCount As Integer
' '读取视图的数量
iViewCount = pView.Application.ActiveDocument.Views.Count
MsgBox "创建了一个新的画面副本 (数量 " & iViewCount & ")。"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

##### WindowStateChange 事件

**描述**
当窗口大小更改时发生（例如，从“最小化”到“最大化”）。

**语法**
`objGDApplication_WindowStateChanged()`

**参数 (可选)**
--

**示例:**
在以下示例中，当窗口大小更改时会输出一条消息：

```vb
Private Sub objGDApplication_WindowStateChanged()
'VBA110
MsgBox "应用程序窗口的状态已更改！"
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)

---

#### 3.6.1.6 方法

**A-C**

##### Activate 方法

**描述**
激活指定的对象。

**语法**
`Expression.Activate()`

**Expression**
必需。一个返回 "Application" 或 "View" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，创建了活动画面的一个副本，然后将其激活：

```vb
Sub CreateAndActivateView()
'VBA111
Dim objView As HMIView
Set objView = ActiveDocument.Views.Add
objView.Activate
End Sub
```

**另请参见**
*   View 对象 (Page 2189)
*   Application 对象 (Page 1993)
*   VBA 参考 (Page 1830)

##### Add 方法

**描述**
向列表中添加另一个元素。

下表显示了可以应用 Add 方法的列表。相应 Add 方法的参数和语法可以在“方法”下找到。

| 列表 | Add 方法的应用 |
| :--- | :--- |
| `AnalogResultInfos` 列表 | 在动态对话框中添加一个新的模拟值范围。 |
| `Documents` 列表 | 在图形编辑器中创建一个新画面。 |
| `GroupedObjects` 列表 | 向组对象中添加一个新对象。 |
| `Toolbars` 列表 | 创建一个新的、用户定义的工具栏。 |
| `Tag Triggers` 列表 | 创建一个新的变量触发器。 |
| `Views` 列表 | 创建指定画面的一个副本。 |

**另请参见**
*   Add 方法 (Views 列表) (Page 1878)
*   Add 方法 (TagTriggers 列表) (Page 1877)
*   Add 方法 (CustomToolbars 列表) (Page 1873)
*   Add 方法 (GroupedObjects 列表) (Page 1876)
*   Add 方法 (Documents 列表) (Page 1875)
*   Add 方法 (AnalogResultInfos 列表) (Page 1871)

##### Add 方法 (AnalogResultInfos 列表)

**描述**
在动态对话框中添加一个新的模拟值范围。

**语法**
`Expression.Add(RangeTo, ResultValue)`

**Expression**
必需。一个返回 "AnalogResultInfos" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `RangeTo` (Variant) | 属性更改所引起的数值范围。 |
| `ResultValue` (Variant) | 当达到该数值范围时，对象属性被赋予的值。 |

**示例:**
在以下示例中，一个圆的半径被动态化，分配了一个变量名并创建了三个模拟值范围：

```vb
Sub AddDynamicDialogToCircleRadiusTypeAnalog()
'VBA112
Dim objDynDialog As HMIDynamicDialog
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle_A", "HMICircle")
Set objDynDialog = objCircle.Radius.CreateDynamic(hmiDynamicCreationTypeDynamicDialog, "'NewDynamic1'")
With objDynDialog
.ResultType = hmiResultTypeAnalog
.AnalogResultInfos.Add 50, 40
.AnalogResultInfos.Add 100, 80
.AnalogResultInfos.ElseCase = 100
End With
End Sub
```
该图显示了执行该过程后的动态对话框：



**另请参见**
*   DynamicDialog 对象 (Page 2038)
*   AnalogResultInfos 对象 (列表) (Page 1992)
*   CreateDynamic 方法 (Page 1911)
*   如何使用动态对话框动态化属性 (Page 1777)

##### Add 方法 (CustomToolbars 列表)

**描述**
创建一个新的、用户定义的工具栏。应用程序级和画面级的用户定义工具栏有所不同：
*   **应用程序级工具栏**: 链接到图形编辑器，并且在图形编辑器中的所有画面都关闭时也可见。将 VBA 代码放在名为 "GlobalTemplateDocument" 或 "ProjectTemplateDocument" 的文档中，并使用 `Application` 属性。
*   **画面级工具栏**: 与特定画面链接，并且只要该画面可见就保持可见。将 VBA 代码放在所需画面的 "ThisDocument" 文档中，并使用 `ActiveDocument` 属性。

**语法**
`Expression.Add(Key)`

**Expression**
必需。一个返回 "CustomToolbars" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Key` (Variant) | 标识用户定义的工具栏。为 "Key" 使用唯一的名称（例如 "DocToolbar1"）。 |

**示例:**
在以下示例中，在活动画面中创建了一个带有两个图标的用户定义工具栏。这些图标由一条分割线分隔：

```vb
Sub AddDocumentSpecificCustomToolbar()
'VBA115
Dim objToolbar As HMIToolbar
Dim objToolbarItem As HMIToolbarItem
 
Set objToolbar = ActiveDocument.CustomToolbars.Add("DocToolbar")
 
'向用户定义的工具栏添加工具栏项
Set objToolbarItem = objToolbar.ToolbarItems.InsertToolbarItem(1, "tItem1_1", "我的第一个符号图标")
Set objToolbarItem = objToolbar.ToolbarItems.InsertToolbarItem(3, "tItem1_3", "我的第二个符号图标")
'
'在两个工具栏项之间插入分割线
Set objToolbarItem = objToolbar.ToolbarItems.InsertSeparator(2, "tSeparator1_2")
End Sub
```

**另请参见**
*   Toolbars 对象 (列表) (Page 2168)
*   InsertToolbarItem 方法 (Page 1940)
*   InsertSeparator 方法 (Page 1937)
*   InsertFromMenuItem 方法 (Page 1932)
*   VBA 参考 (Page 1830)
*   创建自定义菜单和工具栏 (Page 1709)

##### Add 方法 (Documents 列表)

**描述**
在图形编辑器中创建一个新画面。

**语法**
`Expression.Add [HMIOpenDocumentType]`

**Expression**
必需。一个返回 "Documents" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIOpenDocumentType` (HMIDocumentType) | 定义画面将如何打开：<br> - `HMIDocumentTypeVisible`: 打开画面以供直接处理。如果您不指定该参数，这是默认设置。<br> - `HMIDocumentTypeInvisible`: 以不可见模式打开画面，即它不会显示在图形编辑器中。您只能通过 `Documents` 列表来寻址该画面，并通过 `Hide` 属性使其再次可见。 |

**示例:**
在以下示例中，在图形编辑器中创建了一个新画面：

```vb
Sub AddNewDocument()
'VBA113
Application.Documents.Add hmiOpenDocumentTypeVisible
End Sub
```

**另请参见**
*   Hide 属性 (Page 2356)
*   Documents 对象 (列表) (Page 2035)
*   VBA 参考 (Page 1830)

##### Add 方法 (GroupedObjects 列表)

**描述**
将一个现有对象添加到指定的组对象中。

**语法**
`Expression.Add(Index)`

**Expression**
必需。一个返回 "GroupedObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Index` (Variant) | 打算添加的对象。您可以使用索引号或对象名称。 |

**示例:**
在此示例中，从多个对象创建了组对象 "My Group"。然后将一个椭圆段添加到该组对象中：

```vb
Sub CreateGroup()
'VBA114
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim objEllipseSegment As HMIEllipseSegment
Dim objGroup As HMIGroup
 
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
 
MsgBox "对象已选择！"
Set objGroup = ActiveDocument.Selection.CreateGroup
 
'为新组对象设置名称
'该名称标识了组对象
objGroup.ObjectName = "My Group"
'向活动文档添加新对象...
Set objEllipseSegment = ActiveDocument.HMIObjects.AddHMIObject("EllipseSegment", "HMIEllipseSegment")
Set objGroup = ActiveDocument.HMIObjects("My Group")
 '...并将其添加到组中：
objGroup.GroupedHMIObjects.Add ("EllipseSegment")
End Sub
```

**另请参见**
*   GroupedObjects 对象 (列表) (Page 2066)

##### Add 方法 (TagTriggers 列表)

**描述**
创建一个新的变量触发器。

**语法**
`Expression.Add(VarName, Type)`

**Expression**
必需。一个返回 "TagTriggers" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `VarName` (String) | 打算用作触发器的变量名称。请注意，您必须在变量选择对话框中创建该变量。 |
| `Type` (CycleType) | 这是循环类型。当您使用此方法时，在 VBA 编辑器中从列表中选择循环类型。 |

**示例:**
在以下示例中，一个圆的半径使用一个触发变量动态化：

```vb
Sub DynamicWithVariableTriggerCycle()
'VBA69
Dim objVBScript As HMIScriptInfo
Dim objVarTrigger As HMIVariableTrigger
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle_VariableTrigger", "HMICircle")
Set objVBScript = objCircle.Radius.CreateDynamic(hmiDynamicCreationTypeVBScript)
With objVBScript
Set objVarTrigger = .Trigger.VariableTriggers.Add("VarTrigger", hmiVariableCycleType_10s)
.SourceCode = ""
End With
End Sub
```

**另请参见**
*   VariableTriggers 对象 (列表) (Page 2188)
*   VBA 参考 (Page 1830)

##### Add 方法 (Views 列表)

**描述**
创建指定画面的一个副本。

**语法**
`Expression.Add()`

**Expression**
必需。一个返回 "Views" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，创建了活动画面的一个副本，然后将其激活：

```vb
Sub CreateViewAndActivateView()
'VBA117
Dim objView As HMIView
Set objView = ActiveDocument.Views.Add
objView.Activate
End Sub
```

**另请参见**
*   Views 对象 (列表) (Page 2190)
*   VBA 参考 (Page 1830)

##### AddAction 方法

**描述**
在一个对象或属性上组态一个动作。当定义的事件发生时，此动作被触发。

**语法**
`Expression.Method(HMIActionCreationType)`

**Expression**
必需。一个返回 "Actions" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `HMIActionCreationType` (Variant) | 定义动作：<br> - `hmiActionCreationTypeCScript`: 组态一个 C 动作<br> - `hmiActionCreationTypeVBScript`: 组态一个 VBS 动作<br> - `hmiActionCreationTypeDirectConnection`: 组态一个直接连接 |

**示例:**
在以下示例中，组态了一个用于更改圆半径的 VBS 动作：

```vb
Sub AddActionToPropertyTypeVBScript()
'VBA118
Dim objEvent As HMIEvent
Dim objVBScript As HMIScriptInfo
Dim objCircle As HMICircle
'在画面中创建圆。当属性 "Radius" 更改时，
'一个 VBS 动作将被启动：
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle_AB", "HMICircle")
Set objEvent = objCircle.Radius.Events(1)
Set objVBScript = objEvent.Actions.AddAction(hmiActionCreationTypeVBScript)
End Sub
```

**另请参见**
*   Event 对象 (Page 2049)
*   Actions 对象 (列表) (Page 1982)

##### AddActiveXControl 方法

**描述**
向 "HMIObjects" 列表中添加一个新的 `ActiveXControl` 对象。该对象被插入到指定画面的左上角。

**语法**
`Expression.AddActiveXControl("ObjectName", "ProgID")`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectName` (String) | 对象的名称。您可以在列表中通过其名称来寻址该对象。 |
| `ProgID` (String) | 要插入的 ActiveX 控件。 |

**确定 ProgID**
要确定一个 ActiveX 控件的 ProgID，请在图形编辑器中的 "对象面板" 的 "默认" 选项卡下的 "智能对象" 中，将控件对象插入到画面中。"插入控件" 对话框会显示所选控件的路径和 ProgID：



下表显示了由 WinCC 安装的 WinCC 控件的 ProgID 列表：

| WinCC 控件名称 | ProgID |
| :--- | :--- |
| Siemens HMI Symbol Library | SiemensHMI.SymbolLibrary.1 |
| WinCC AlarmControl | CCAxAlarmControl.AxAlarmControl.1 |
| WinCC digital/analog clock control | DACLOCK.DaclockCtrl.1 |
| WinCC FunctionTrendControl | CCAxFunctionTrendControl.AxFunctionTrendControl.1 |
| WinCC gauge control | XGAUGE.XGaugeCtrl.1 |
| WinCC media control | CCMediaControl.CCMediaControl.1 |
| WinCC OnlineTableControl | CCAxOnlineTableControl.AxOnlineTableControl.1 |
| WinCC OnlineTrendControl | CCAxOnlineTrendControl.AxOnlineTrendControl.1 |
| WinCC Push Button Control (up to WinCC V7.3) | PBUTTON.PbuttonCtrl.1 |
| WinCC slider control | SLIDER.SliderCtrl.1 |
| WinCC RulerControl | CCAxTrendRulerControl.AxRulerControl.1 |
| WinCC UserArchiveControl | CCAxUserArchiveControl.AxUserArchiveControl.1 |

**示例:**
在以下示例中，ActiveX 控件 "WinCC Gauge Control" 被插入到活动画面中。

```vb
Sub AddActiveXControl()
'VBA119
Dim objActiveXControl As HMIActiveXControl
Set objActiveXControl = ActiveDocument.HMIObjects.AddActiveXControl("WinCC_Gauge", "XGAUGE.XGaugeCtrl.1")
With ActiveDocument
.HMIObjects("WinCC_Gauge").Top = 40
.HMIObjects("WinCC_Gauge").Left = 40
End With
End Sub
```

> **注意**
>
> 执行该方法后，图形编辑器将不会完全关闭。"Grafexe.exe" 文件保留在内存中。为了重新启动图形编辑器，请在任务管理器中退出 "Grafexe.exe" 应用程序。

**另请参见**
*   ActiveX 控件 (Page 1757)
*   HMIObjects 对象 (列表) (Page 2072)
*   ActiveXControl 对象 (Page 1984)
*   VBA 参考 (Page 1830)

##### AddDotNetControl 方法

**描述**
向 "HMIObjects" 列表中添加一个新的 ".Net-Control" 对象。

**语法**
`Expression.AddDotNetControl(ObjectName, ControlType, InGAC, AssemblyInfo)`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectName` (String) | 对象的名称。您可以在列表中通过其名称来寻址该对象。 |
| `ControlType` (String) | 对象的命名空间。 |
| `InGAC` (String) | `TRUE`: 对象已在全局程序集缓存中注册。<br> `FALSE`: 对象未在全局程序集缓存中注册。 |
| `AssemblyInfo` (String) | 如果 "InGAC=TRUE"，则将指定以下信息：<br> - Assembly<br> - Version<br> - Culture<br> - PublicKeyToken<br> 如果 "InGAC=FALSE"，则只在 "Assembly" 中指定对象的路径。 |

**示例**
在以下示例中，来自全局程序集缓存的 ".NETControl" 对象被插入到活动画面中。

```vb
'VBA851
Dim DotNetControl As HMIDotNetControl 
Set DotNetControl = ActiveDocument.HMIObjects.AddDotNetControl("MyVBAControl", "System.Windows.Forms.Label", True,"Assembly=System.Windows.Forms, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089")
```

##### AddFolder 方法

**描述**
在组件库中创建一个新文件夹。类型为 "Folder" 的 `FolderItem` 对象被添加到 `FolderItems` 列表中。

新创建的文件夹会接收内部名称 "FolderX"，其中 "X" 代表一个从 1 开始的连续数字。使用内部名称在 `FolderItems` 列表中寻址该文件夹。

**语法**
`Expression.AddFolder(DefaultName)`

**Expression**
必需。一个返回 "FolderItems" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `DefaultName` (String) | 要创建的文件夹的名称。 |

**示例:**
在以下示例中，将在 "项目库" 中创建文件夹 "My Folder"：

```vb
Sub AddNewFolderToProjectLibrary()
'VBA120
Dim objProjectLib As HMISymbolLibrary
Set objProjectLib = Application.SymbolLibraries(2)
objProjectLib.FolderItems.AddFolder ("My Folder")
End Sub
```

**另请参见**
*   SymbolLibrary 对象 (Page 2162)
*   FolderItems 对象 (列表) (Page 2056)
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### AddFromClipboard 方法

**描述**
将剪贴板中的一个对象复制到组件库的一个文件夹中。类型为 "Item" 的 `FolderItem` 对象被添加到 `FolderItems` 列表中。

> **注意**
>
> 剪贴板必须包含来自图形编辑器的对象。其他内容（如 ASCII 文本）将不会被粘贴。

**语法**
`Expression.AddFromClipboard(DefaultName)`

**Expression**
必需。一个返回 "FolderItems" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `DefaultName` (String) | 要赋予粘贴到组件库中的对象的名称。 |

**示例:**
在以下示例中，来自 "全局库" 的对象 "PC" 将被复制到 "项目库" 的文件夹 "Folder 3" 中：

```vb
Sub CopyObjectFromGlobalLibraryToProjectLibrary()
'VBA121
Dim objGlobalLib As HMISymbolLibrary
Dim objProjectLib As HMISymbolLibrary
Set objGlobalLib = Application.SymbolLibraries(1)
Set objProjectLib = Application.SymbolLibraries(2)
objProjectLib.FolderItems.AddFolder ("My Folder3")
'
'将对象从 "全局库" 复制到剪贴板
With objGlobalLib
.FolderItems(2).Folder.Item(2).Folder.Item(1).CopyToClipboard
End With
' '将对象从剪贴板粘贴到 "项目库"
objProjectLib.FolderItems(objProjectLib.FindByDisplayName("My Folder3").Name).Folder.AddFromClipBoard ("Copy of PC/PLC")
End Sub
```

**另请参见**
*   FolderItems 对象 (列表) (Page 2056)
*   SymbolLibrary 对象 (Page 2162)
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### AddHMIObject 方法

**描述**
向 "HMIObjects" 列表中添加一个新的标准、智能或 Windows 对象。该对象被插入到指定画面的左上角。

> **注意**
>
> 使用 `AddActiveXControl` 方法插入 ActiveXControl。
> 使用 `AddOLEObject` 方法插入 OLE 元素。

**语法**
`Expression.AddHMIObject("ObjectName", "ProgID")`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectName` (String) | 对象的名称。您可以在列表中通过其名称来寻址该对象。 |
| `ProgID` (String) | 要插入的对象类型。通过在 VBA 对象名前加上 "HMI" 来获取 "ProgID"（例如 HMICircle 或 HMIRectangle）。 |

**示例:**
在以下示例中，将在活动画面中插入一个圆，并将其背景色设置为 "红色"：

```vb
Sub AddCircleToActiveDocument()
'VBA122
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("VBA_Circle", "HMICircle")
objCircle.BackColor = RGB(255, 0, 0)
End Sub
```

**另请参见**
*   PieSegment 对象 (Page 2114)
*   TextList 对象 (Page 2163)
*   StatusDisplay 对象 (Page 2152)
*   StaticText 对象 (Page 2149)
*   Slider 对象 (Page 2144)
*   RoundRectangle 对象 (Page 2137)
*   RoundButton 对象 (Page 2133)
*   Rectangle 对象 (Page 2131)
*   PolyLine 对象 (Page 2120)
*   PictureWindow 对象 (Page 2111)
*   OptionGroup 对象 (Page 2107)
*   HMIObjects 对象 (列表) (Page 2072)
*   Line 对象 (Page 2086)
*   IOField 对象 (Page 2074)
*   GraphicObject 对象 (Page 2058)
*   EllipseArc 对象 (Page 2043)
*   EllipseSegment 对象 (Page 2046)
*   Ellipse 对象 (Page 2040)
*   CircularArc 对象 (Page 2014)
*   Circle 对象 (Page 2011)
*   CheckBox 对象 (Page 2008)
*   Button 对象 (Page 2004)
*   BarGraph 对象 (Page 1998)
*   ApplicationWindow 对象 (Page 1995)
*   AddOLEObject 方法 (Page 1888)
*   AddActiveXControl 方法 (Page 1880)
*   VBA 参考 (Page 1830)

##### AddItem 方法

**描述**
将指定画面的一个对象复制到组件库的一个文件夹中。类型为 "Item" 的 `FolderItem` 对象被添加到 `FolderItems` 列表中。

**语法**
`Expression.Folder.AddItem "DefaultName", pHMIObject`

**Expression**
必需。一个返回 "FolderItems" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `DefaultName` (String) | 要赋予粘贴到组件库中的对象的名称。 |
| `pHMIObject` (HMIObject) | 要从指定画面插入到组件库中的对象。 |

**示例:**
在以下示例中，一个圆将被复制到 "项目库"。为此，该圆将被粘贴到活动画面中，并且文件夹 "My Folder 2" 将被创建。

```vb
Sub VBA123()
'VBA123
Dim objProjectLib As HMISymbolLibrary
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle", "HMICircle")
Set objProjectLib = Application.SymbolLibraries(2)
objProjectLib.FolderItems.AddFolder ("My Folder2")
objProjectLib.FindByDisplayName("My Folder2").Folder.AddItem "ProjectLib Circle", ActiveDocument.HMIObjects("Circle")
End Sub
```

**另请参见**
*   FolderItems 对象 (列表) (Page 2056)
*   SymbolLibrary 对象 (Page 2162)
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### AddOLEObject 方法

**描述**
向 "HMIObjects" 列表中添加一个新的 OLE 元素。该对象被插入到指定画面的左上角。

**语法**
`Expression.AddOLEObject(ObjectName, ServerName, [CreationType], [UseSymbol])`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectName` (String) | 对象的名称。您可以在列表中通过其名称来寻址该对象。 |
| `ServerName` (String) | 将包含 OLE 元素的应用程序的名称，或带完整路径的文件名。"ServerName" 的值对应于 "插入对象" 对话框中的 "对象类型"。 |
| `CreationType` (HMIOLEObjectCreationType) | 定义 OLE 元素是新建还是使用现有文件：<br> - `HMIOLEObjectCreationTypeDirect`: 对应于设置 "新建"。如果您不指定该参数，则使用此设置。<br> - `HMIOLEObjectCreationTypeByLink`: 对应于设置 "从文件创建"。这将创建文件的一个副本。对 OLE 元素的任何更改都不会影响原始文件。通过 "ServerName" 参数为文件分配一个名称。<br> - `HMIOLEObjectCreationTypeByLinkWithReference`: 与上述相同，只是对 OLE 元素的更改会影响原始文件。通过 "ServerName" 参数为文件分配一个名称。 |
| `UseSymbol` (Boolean) | 如果要使用文件类型的标准图标，则为 `TRUE`。双击该图标将打开关联的应用程序。此参数的默认设置为 `FALSE`。 |

**示例:**
在以下示例中，一个包含 Wordpad 文档的 OLE 元素将被插入到活动画面中：

```vb
Sub AddOLEObjectToActiveDocument()
'VBA124
Dim objOLEObject As HMIOLEObject
Set objOLEObject = ActiveDocument.HMIObjects.AddOLEObject("MS Wordpad Document", "Wordpad.Document.1")
End Sub
```
在以下示例中，将使用 `AddOLEObject` 方法并指定 "HMIOLEObjectCreationTypeByLink" 参数：

```vb
Sub AddOLEObjectByLink()
'VBA805
Dim objOLEObject As HMIOLEObject
Dim strFilename As String
'
'通过文件名添加 OLEObject。在这种情况下，文件名必须
'包含文件名和路径。
'用您系统上存在的带路径的文件名替换 strFilename 的定义
strFilename = Application.ApplicationDataPath & "Test.bmp"
Set objOLEObject = ActiveDocument.HMIObjects.AddOLEObject("OLEObject1", strFilename, hmiOLEObjectCreationTypeByLink, False)
End Sub
```
在以下示例中，将使用 `AddOLEObject` 方法并指定 "HMIOLEObjectCreationTypeByLinkWithReference" 参数：

```vb
Sub AddOLEObjectByLinkWithReference()
'VBA806
Dim objOLEObject As HMIOLEObject
Dim strFilename As String
'
'通过文件名添加 OLEObject。在这种情况下，文件名必须
'包含文件名和路径。
'用您系统上存在的带路径的文件名替换 strFilename 的定义
strFilename = Application.ApplicationDataPath & "Test.bmp"
Set objOLEObject = ActiveDocument.HMIObjects.AddOLEObject("OLEObject1", strFilename, hmiOLEObjectCreationTypeByLinkWithReference, True)
End Sub
```

**另请参见**
*   OLEObject 对象 (Page 2105)
*   HMIObjects 对象 (列表) (Page 2072)
*   VBA 参考 (Page 1830)

##### AddWPFControl 方法

**描述**
向 "HMIObjects" 列表中添加一个新的 "WPF-Control" 对象。

**语法**
`Expression.AddWPFControl(ObjectName, ControlType, InGAC, AssemblyInfo)`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectName` (String) | 对象的名称。您可以在列表中通过其名称来寻址该对象。 |
| `ControlType` (String) | 对象的命名空间。 |
| `InGAC` (String) | `TRUE`: 对象已在全局程序集缓存中注册。<br> `FALSE`: 对象未在全局程序集缓存中注册。 |
| `AssemblyInfo` (String) | 如果 "InGAC=TRUE"，则将指定以下信息：<br> - Assembly<br> - Version<br> - Culture<br> - PublicKeyToken<br> 如果 "InGAC=FALSE"，则只在 "Assembly" 中指定对象的路径。 |

**示例**
在以下示例中，来自全局程序集缓存外部的 "WPF Control" 对象被插入到活动画面中。

```vb
'VBA852
Dim WPFControl As HMIWPFControl
Set WPFControl = ActiveDocument.HMIObjects.AddWPFControl("MyWPFVBAControl", "WinCCWPFControl.TestControl", False, "Assembly=Z:\TestControl\WinCCWPFControl.dll")
```

##### AlignBottom 方法

**描述**
将指定画面中选定的对象与底边对齐。对齐方式以您选择的第一个对象为基准。

**语法**
`Expression.AlignBottom()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后将它们与底边对齐：

```vb
Sub AlignSelectedObjectsBottom()
'VBA125
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.AlignBottom
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### AlignLeft 方法

**描述**
将指定画面中选定的对象左对齐。对齐方式以您选择的第一个对象为基准。

**语法**
`Expression.AlignLeft()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后将它们左对齐：

```vb
Sub AlignSelectedObjectsLeft()
'VBA126
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.AlignLeft
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### AlignRight 方法

**描述**
将指定画面中选定的对象右对齐。对齐方式以您选择的第一个对象为基准。

**语法**
`Expression.AlignRight()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后将它们右对齐：

```vb
Sub AlignSelectedObjectsRight()
'VBA127
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.AlignRight
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### AlignTop 方法

**描述**
将指定画面中选定的对象与顶边对齐。对齐方式以您选择的第一个对象为基准。

**语法**
`Expression.AlignTop()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后将它们与顶边对齐：

```vb
Sub AlignSelectedObjectsTop()
'VBA128
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.AlignTop
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### ArrangeMinimizedWindows 方法

**描述**
将所有最小化的画面排列在图形编辑器的下边缘。

**语法**
`Expression.ArrangeMinimizedWindows()`

**Expression**
必需。一个返回 "Application" 类型对象的表达式或元素。

**示例:**
在以下示例中，所有最小化的画面都被排列在图形编辑器的下边缘。要使此示例正常工作，您必须已在图形编辑器中最小化了多个画面：

```vb
Sub ArrangeMinimizedWindows()
'VBA129
Application.ArrangeMinimizedWindows
End Sub
```

**另请参见**
*   Application 对象 (Page 1993)
*   VBA 参考 (Page 1830)

##### BackwardOneLevel 方法

**描述**
将选定的对象在其当前图层内向后移动一个层级。

**语法**
`Expression.BackwardOneLevel()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在活动画面中插入了两个对象。最后插入的对象然后被向后移动一个层级：

```vb
Sub MoveObjectOneLevelBackward()
'VBA173
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = False
End With
With objRectangle
.Top = 40
.Left = 40
.Width = 100
.Height = 50
.BackColor = RGB(255, 0, 255)
.Selected = True
End With
MsgBox "对象已创建并选择！"
ActiveDocument.Selection.BackwardOneLevel
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### BringToFront 方法

**描述**
将选定的对象在其当前图层内移到最前面。

> **注意**
>
> 如果使用 "BringToFront" 方法，HMIObjects 列表中的 HMI 对象序列可能会改变。

**语法**
`Expression.BringToFront()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在活动画面中插入了两个对象。最后插入的对象然后被移到最前面：

```vb
Sub MoveObjectToFront()
'VBA198
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 40
.Left = 40
.Width = 100
.Height = 50
.BackColor = RGB(255, 0, 255)
.Selected = False
End With
MsgBox "圆形和矩形对象已创建" & vbCrLf & "只有圆形被选中！"
ActiveDocument.Selection.BringToFront
MsgBox "选中的对象已被移到最前面。"
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### CascadeWindows 方法

**描述**
将图形编辑器中所有打开的画面以层叠方式（即重叠）排列。

**语法**
`Expression.Methode(Parameter)`

**Expression**
必需。一个返回 "Application" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，图形编辑器中所有打开的画面都以层叠方式排列。要使此示例正常工作，您必须已在图形编辑器中打开了多个画面：

```vb
Sub CascadeWindows()
'VBA130
Application.CascadeWindows
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   Application 对象 (Page 1993)

##### CenterHorizontally 方法

**描述**
使用此方法，指定画面中选定的对象将水平居中。

**语法**
`Expression.CenterHorizontally()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后水平居中：

```vb
Sub CenterSelectedObjectsHorizontally()
'VBA131
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.CenterHorizontally
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### CenterVertically 方法

**描述**
使用此方法，指定画面中选定的对象将垂直居中。

**语法**
`Expression.CenterVertically()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后垂直居中：

```vb
Sub CenterSelectedObjectsVertically()
'VBA132
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.CenterVertically
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### CheckSyntax 方法

**描述**
检查指定 C 脚本的语法是否正确。
与 `Compiled` 属性结合使用 `CheckSyntax` 方法。

**语法**
`Expression.CheckSyntax(CheckOK, Error)`

**Expression**
必需。一个返回 "DynamicDialog" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `CheckOK` (Boolean) | 如果指定 C 脚本的语法正确，则为 `TRUE`。 |
| `Error` (String) | 如果 C 脚本不正确，则输出的消息文本。 |

**示例:**
--

**另请参见**
*   DynamicDialog 对象 (Page 2038)
*   VBA 参考 (Page 1830)

##### Close 方法

**描述**
关闭指定的画面并将其从文档列表中移除。

> **注意**
>
> 未保存的更改将会丢失。

**语法 1**
`Expression.Close(FileName)`

**Expression**
必需。一个返回 "Documents" 类型对象的表达式或元素。

**语法 2**
`Expression.Close()`

**Expression**
必需。一个返回 "Document" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `FileName` (String) | 要关闭的 PDL 文件的名称。 |

**示例:**
在以下示例中，将关闭画面 "Test.PDL"。要使此示例正常工作，您必须已打开画面 "Test.PDL"：

```vb
Sub CloseDocumentUsingTheFileName()
'VBA134
Dim strFile As String
strFile = Application.ApplicationDataPath & "test.pdl"
Application.Documents.Close (strFile)
End Sub
```
在以下示例中，将关闭图形编辑器中的活动画面：

```vb
Sub CloseDocumentUsingActiveDocument()
'VBA135
ActiveDocument.Close
End Sub
```

**另请参见**
*   Document 对象 (Page 2032)
*   ActiveDocument 属性 (Page 2194)
*   Documents 对象 (列表) (Page 2035)
*   VBA 参考 (Page 1830)

##### CloseAll 方法

**描述**
关闭在图形编辑器中打开的所有画面，并将它们从文档列表中移除。

> **注意**
>
> 未保存的更改将会丢失。

**语法**
`Expression.CloseAll()`

**Expression**
必需。一个返回 "Documents" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，关闭了图形编辑器中所有打开的画面：

```vb
Sub CloseAllDocuments()
'VBA136
Application.Documents.CloseAll
End Sub
```

**另请参见**
*   Documents 对象 (列表) (Page 2035)
*   VBA 参考 (Page 1830)

##### ConvertToScript 方法

**描述**
将指定的动态对话框转换为 C 脚本。
转换时，关联的 `DynamicDialog` 对象将被删除。

> **注意**
>
> 您无法撤销转换。

**语法**
`Expression.ConvertToScript()`

**Expression**
必需。一个返回 "DynamicDialog" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，将在活动画面中插入一个圆，并使用动态对话框动态组态其半径。然后，动态对话框将被转换为 C 脚本。

```vb
Sub ConvertDynamicDialogToScript()
'VBA137
Dim objDynDialog As HMIDynamicDialog
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle_A", "HMICircle")
'
'创建动态
Set objDynDialog = objCircle.Radius.CreateDynamic(hmiDynamicCreationTypeDynamicDialog, "'NewDynamic1'")
' '组态动态。"ResultType" 定义了数值范围类型：
With objDynDialog
.ResultType = hmiResultTypeAnalog
.AnalogResultInfos.Add 50, 40
.AnalogResultInfos.Add 100, 80
.AnalogResultInfos.ElseCase = 100
MsgBox "动态对话框将被更改为 C 脚本。"
.ConvertToScript
End With
End Sub```

**另请参见**
*   DynamicDialog 对象 (Page 2038)
*   VBA 参考 (Page 1830)

##### ConvertToVBScript 方法

**描述**
将指定的动态对话框转换为 VB 脚本。
转换时，关联的 `DynamicDialog` 对象将被删除。

> **注意**
>
> 您无法撤销转换。

**语法**
`Expression.ConvertToVBScript()`

**Expression**
必需。一个返回 "DynamicDialog" 类型对象的表达式。

**参数**
--

**示例**
在以下示例中，一个圆被插入到活动画面中，并使用动态对话框动态组态其半径。然后，动态对话框被转换为 VB 脚本。

```vb
Sub ConvertDynamicDialogToVBScript()
'VBA908
Dim objDynDialog As HMIDynamicDialog
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("Circle_A", "HMICircle")
'
'创建动态
Set objDynDialog = objCircle.Radius.CreateDynamic(hmiDynamicCreationTypeDynamicDialog, "'NewDynamic1'")
' '组态动态。"ResultType" 定义了数值范围类型：
With objDynDialog
.ResultType = hmiResultTypeAnalog
.AnalogResultInfos.Add 50, 40
.AnalogResultInfos.Add 100, 80
.AnalogResultInfos.ElseCase = 100
MsgBox "动态对话框将被更改为 VB 脚本。"
.ConvertToVBScript
End With
End Sub
```

**另请参见**
*   DynamicDialog 对象 (Page 2038)

##### ConvertWM 方法

**描述**
内部用于 PowerCC。

##### CopySelection 方法

**描述**
使用此方法，画面中选定的对象被复制到剪贴板。

**语法**
`Expression.CopySelection()`

**Expression**
必需。一个返回 "Document" 或 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，选择了活动画面中插入的两个对象。选择内容被复制并粘贴到一个新画面中：

```vb
Sub CopySelectionToNewDocument()
'VBA138
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim iNewDoc As Integer
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
'除了 "ActiveDocument.CopySelection"，您也可以写：
'"ActiveDocument.Selection.CopySelection"。
ActiveDocument.CopySelection
Application.Documents.Add hmiOpenDocumentTypeVisible
iNewDoc = Application.Documents.Count
Application.Documents(iNewDoc).PasteClipboard
End Sub
```

**另请参见**
*   Document 对象 (Page 2032)
*   ActiveDocument 属性 (Page 2194)
*   SelectedObjects 对象 (列表) (Page 2141)
*   PasteClipboard 方法 (Page 1952)
*   Add 方法 (Documents 列表) (Page 1875)
*   Activate 方法 (Page 1870)
*   VBA 参考 (Page 1830)

##### CopyToClipboard 方法

**描述**
将组件库中一个文件夹里的对象复制到剪贴板。

**语法**
`Expression.CopyToClipboard()`

**Expression**
必需。一个返回类型为 "Item" 的 `FolderItem` 对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，来自 "全局库" 的对象 "PC" 将被复制到 "项目库" 的文件夹 "My Folder3" 中：

```vb
Sub CopyObjectFromGlobalLibraryToProjectLibrary()
'VBA139
Dim objGlobalLib As HMISymbolLibrary
Dim objProjectLib As HMISymbolLibrary
Dim objFolderItem As HMIFolderItem
 
Set objGlobalLib = Application.SymbolLibraries(1)
Set objProjectLib = Application.SymbolLibraries(2)
objProjectLib.FolderItems.AddFolder ("My Folder3")
'
'将对象从 "全局库" 复制到剪贴板
With objGlobalLib
.FolderItems(2).Folder.Item(2).Folder.Item(1).CopyToClipboard
End With
'
'将对象从剪贴板粘贴到 "项目库"
Set objFolderItem = objProjectLib.FindByDisplayName("My Folder3")
objFolderItem.Folder.AddFromClipBoard ("Copy of PC/PLC")
End Sub
```

**另请参见**
*   SymbolLibrary 对象 (Page 2162)
*   FolderItem 对象 (Page 2055)
*   VBA 参考 (Page 1830)
*   使用 VBA 访问组件库 (Page 1728)

##### CreateCustomizedObject 方法

**描述**
从指定画面中选定的对象创建一个自定义对象。然后您必须在 "组态对话框" 中组态该自定义对象。
有关此主题的更多信息，请参阅本文档中的 "自定义对象" 和 WinCC 文档中的 "自定义对象"。

**语法**
`Expression.CreateCustomizedObject()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后创建一个自定义对象：

```vb
Sub CreateCustomizedObject()
'VBA140
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim objCustObject As HMICustomizedObject
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
Set objCustObject = ActiveDocument.Selection.CreateCustomizedObject
objCustObject.ObjectName = "myCustomizedObject"
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   CustomizedObject 对象 (Page 2021)
*   VBA 参考 (Page 1830)
*   自定义对象 (Page 1768)

##### CreateDynamicDialog 方法

**描述**
根据特定的值范围或变量状态动态化画面和对象的属性。

**语法**
`Expression.CreateDynamicDialog([Code as String],iResultType as Long)`

**Expression**
必需。一个返回 "Property" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Code` (String) | 定义用于动态目的的函数或变量。同时以单引号指定变量名称："'变量名'" |
| `iResultType` (Long) | 定义值范围的类型：<br> - `hmiResultTypeDirect` = 0<br> - `hmiResultTypeAnalog` = 1<br> - `hmiResultTypeBool` = 2<br> - `hmiResultTypeBit` = 3 |

**示例**
在以下示例中，一个圆的半径被动态对话框动态化。一个变量名和一个 "ResultType" 被分配给该动态对话框。

```vb
Sub AddDynamicDialogToCircleRadiusTypeAnalog()
'VBA820
Dim objDynDialog As HMIDynamicDialog
Dim objCircle As HMICircle
'创建对象
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("myCircle","HMICircle")
'创建动态 (变量 "myTest" 必须存在)
Set objDynDialog = objCircle.Radius.CreateDynamicDialog("'myTest'",0)
End Sub
```

**另请参见**
*   FaceplateProperty 对象 (Page 2054)

##### CreateDynamic 方法

**描述**
使指定的属性动态化。

**语法**
`Expression.CreateDynamic(DynamicType, [SourceCode])`

**Expression**
必需。一个返回 "Property" 类型对象的表达式或元素。

**参数**
只有当您希望借助动态对话框使指定属性动态化时，才需要使用 "SourceCode" 参数。
在所有其他类型的动态化中，您可以省略该参数。

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `DynamicType` (HMIDynamicCreationType) | 定义动态的类型：<br> - `hmiDynamicCreationTypeVariableDirect`: 使用变量的动态<br> - `hmiDynamicCreationTypeVariableIndirect`: 使用变量的动态。在这种类型的动态中，您只指定其值将用于动态目的的变量的名称。<br> - `hmiDynamicCreationTypeScript`: 使用脚本（C、VB）的动态。<br> - `hmiDynamicCreationTypeDynamicDialog`: 使用动态对话框进行动态化。 |
| `SourceCode` (String) | 定义将用于动态目的的函数或变量。也以单引号指定变量名称："'变量名'" |

**示例:**
在此示例中，一个圆的属性 "Top" 将借助变量 "NewDynamic" 动态化：

```vb
Sub AddDynamicAsVariableDirectToProperty()
'VBA141
Dim objVariableTrigger As HMIVariableTrigger
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("MyCircle", "HMICircle")
'使属性 "Top" 动态化：
Set objVariableTrigger = objCircle.Top.CreateDynamic(hmiDynamicCreationTypeVariableDirect, "NewDynamic")
'
'定义循环时间
With objVariableTrigger
.CycleType = hmiCycleType_2s
End With
End Sub
```

**另请参见**
*   Property 对象 (Page 2124)
*   DeleteDynamic 方法 (Page 1916)
*   VBA 参考 (Page 1830)

##### CreateGroup 方法

**描述**
从指定画面中选定的对象创建一个组对象。
有关此主题的更多信息，请参阅本文档中的 "组对象" 和 WinCC 文档中的 "组对象"。

**语法**
`Expression.CreateGroup()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入两个对象，然后创建一个组对象：

```vb
Sub CreateGroup()
'VBA142
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim objGroup As HMIGroup
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
Set objGroup = ActiveDocument.Selection.CreateGroup
objGroup.ObjectName = "myGroup"
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   Group 对象 (Page 2061)
*   VBA 参考 (Page 1830)
*   组对象 (Page 1760)

**D-M**

##### Delete 方法

**描述**
删除指定的对象并将其从列表中移除。

**语法**
`Expression.Delete()`

**Expression**
必需。一个返回以下类型对象的表达式或元素：
*   Assignment
*   FolderItem
*   LanguageText
*   Menu
*   MenuItem
*   Object
*   Toolbar
*   ToolbarItem
*   VariableTrigger
*   View

**参数**
--

**示例:**
在以下示例中，将删除活动画面中的第一个对象。要使此示例正常工作，您必须已在活动画面中创建了至少一个对象：

```vb
Sub ObjectDelete()
'VBA143
ActiveDocument.HMIObjects(1).Delete
End Sub
```

**另请参见**
*   LanguageText 对象 (Page 2081)
*   View 对象 (Page 2189)
*   VariableTrigger 对象 (Page 2186)
*   ToolbarItem 对象 (Page 2170)
*   FolderItem 对象 (Page 2055)
*   HMIObject 对象 (Page 2070)
*   MenuItem 对象 (Page 2095)
*   Menu 对象 (Page 2091)
*   VBA 参考 (Page 1830)

##### DeleteAll 方法

**描述**
删除指定画面中所有选定的对象，并将它们从 "Selection" 和 "HMIObjects" 列表中移除。

**语法**
`Expression.DeleteAll()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，首先插入并选择了两个对象，然后将它们删除：

```vb
Sub DeleteAllSelectedObjects()
'VBA145
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已选择！"
ActiveDocument.Selection.DeleteAll
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### DeleteDynamic 方法

**描述**
从指定的属性中移除动态特性。

**语法**
`Expression.DeleteDynamic`

**Expression**
必需。一个返回 "Property" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，将移除借助 `CreateDynamic` 方法创建的动态特性：

```vb
Sub DeleteDynamicFromObjectMeinKreis()
'VBA146
Dim objCircle As HMICircle
Set objCircle = ActiveDocument.HMIObjects("MyCircle")
objCircle.Top.DeleteDynamic
End Sub
```

**另请参见**
*   Property 对象 (Page 2124)
*   CreateDynamic 方法 (Page 1911)
*   VBA 参考 (Page 1830)

##### DeselectAll 方法

**描述**
取消选择指定画面中所有选定的对象，并将它们从 `Selection` 列表中移除。

**语法**
`Expression.DeselectAll()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，首先插入并选择了两个对象。然后取消选择所有选定的对象：

```vb
Sub SelectObjectsAndDeselectThemAgain()
'VBA147
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已创建并选择！"
ActiveDocument.Selection.DeselectAll
MsgBox "对象已取消选择！"
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### Destroy 方法

**描述**
解组指定的自定义对象。对象保持不变。

**语法**
`Expression.Destroy()`

**Expression**
一个返回 "CustomizedObject" 类型对象的表达式或元素。

**参数**
--

**示例**
显示如何使用 `Destroy` 方法的示例可以在本文档的“使用 VBA 编辑自定义对象”标题下找到。

**另请参见**
*   CustomizedObject 对象 (Page 2021)
*   Destroy 方法 (Page 1918)
*   Delete 方法 (Page 1914)
*   CreateCustomizedObject 方法 (Page 1909)
*   如何使用 VBA 编辑自定义对象 (Page 1769)

##### DuplicateSelection 方法

**描述**
复制指定画面中选定的对象。这样创建的对象被添加到 `HMIObjects` 列表中。每次复制时，新对象的名称会按顺序编号。
例如，如果您复制一个名为 "Circle" 的对象，复制的对象将被称为 "Circle1"。如果您再次复制名为 "Circle" 的对象，生成的对象将被称为 "Circle2"，依此类推。

**语法**
`Expression.DuplicateSelection()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，插入并选择了两个对象。然后它们被复制：

```vb
Sub DuplicateSelectedObjects()
'VBA149
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 80
.Selected = True
End With
MsgBox "对象已创建并选择！"
ActiveDocument.Selection.DuplicateSelection
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   HMIObjects 对象 (列表) (Page 2072)
*   VBA 参考 (Page 1830)

##### EvenlySpaceHorizontally 方法

**描述**
使用此方法，指定画面中选定的对象将以均匀的距离水平间隔。

**语法**
`Expression.EvenlySpaceHorizontally()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入三个对象并选择它们。然后它们被以均匀的距离水平放置：

```vb
Sub EvenlySpaceObjectsHorizontally()
'VBA150
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim objEllipse As HMIEllipse
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
Set objEllipse = ActiveDocument.HMIObjects.AddHMIObject("sEllipse", "HMIEllipse")
With objCircle
.Top = 30
.Left = 0
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 42
.Selected = True
End With
With objEllipse
.Top = 48
.Left = 162
.BackColor = RGB(255, 0, 0)
.Selected = True
End With
MsgBox "对象已创建并选择！"
ActiveDocument.Selection.EvenlySpaceHorizontally
End Sub
```

**另请参见**
*   VBA 参考 (Page 1830)
*   SelectedObjects 对象 (列表) (Page 2141)

##### EvenlySpaceVertically 方法

**描述**
使用此方法，指定画面中选定的对象将以均匀的距离垂直间隔。

**语法**
`Expression.EvenlySpaceVertically()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在当前画面中的不同位置插入三个对象并选择它们。然后它们被以均匀的距离垂直放置：

```vb
Sub EvenlySpaceObjectsVertically()
'VBA151
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Dim objEllipse As HMIEllipse
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
Set objEllipse = ActiveDocument.HMIObjects.AddHMIObject("sEllipse", "HMIEllipse")
With objCircle
.Top = 30
.Left = 0
.Selected = True
End With
With objRectangle
.Top = 80
.Left = 42
.Selected = True
End With
With objEllipse
.Top = 48
.Left = 162
.BackColor = RGB(255, 0, 0)
.Selected = True
End With
MsgBox "对象已创建并选择"
ActiveDocument.Selection.EvenlySpaceVertically
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### Export 方法

**描述**
将指定的画面另存为 EMF 文件。

**语法**
`Expression.Export(Type, Path)`

**Expression**
必需。一个返回 "Document" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Type` (HMIImportExportType) | 定义导出的画面将以何种格式保存。 |
| `Path` (String) | 画面将要导出的路径。该路径必须存在。 |

**示例**

```vb
Sub ExportAllPicturesAsPDL()
'VBA152
Dim iPictureCounter As Integer
Dim strPath As String
strPath = "C:\WinCC_PDL_Export\"
'计算图形编辑器中的画面数量...
For iPictureCounter = 1 To grafexe.Documents.Count
    '...并将每个画面作为 PDL 文件导出到指定路径：
    grafexe.Documents(iPictureCounter).Export hmiImportExportTypePDL, strPath
Next iPictureCounter
End Sub
```

**另请参见**
*   View 对象 (Page 2189)
*   Document 对象 (Page 2032)

##### Find 方法

**描述**
在指定的画面中搜索对象，并以集合对象的形式返回搜索结果。您可以搜索以下对象属性：
*   Type
*   Name
*   Property

**语法**
`Expression.Find([ObjectType], [ObjectName], [PropertyName])`

**Expression**
必需。一个返回 "HMIObjects" 类型对象的表达式或元素。

**参数**
您必须至少指定三个参数中的一个。

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `ObjectType` (String) | 要搜索的对象类型。指定相关对象的 "ProgID"。通过在 VBA 对象名前加上 "HMI" 来获取 "ProgID"（例如 HMICircle 或 HMIRectangle）。 |
| `ObjectName` (String) | 要搜索的对象的名称。您可以在对象名称中使用占位符 (?,*) 来查找名称相似的对象。 |
| `PropertyName` (String) | 要搜索的对象属性的名称。指定相关的 VBA 属性名称（例如，用 "BackColor" 代替 "Background Color"）。 |

**示例:**
在以下示例中，将在活动画面中搜索 "HMICircle" 类型的对象，并输出搜索结果：

```vb
Sub FindObjectsByType()
'VBA153
Dim colSearchResults As HMICollection
Dim objMember As HMIObject
Dim iResult As Integer
Dim strName As String
Set colSearchResults = ActiveDocument.HMIObjects.Find(ObjectType:="HMICircle")
For Each objMember In colSearchResults
iResult = colSearchResults.Count
strName = objMember.ObjectName
MsgBox "找到: " & CStr(iResult) & vbCrLf & "对象名称: " & strName)
Next objMember
End Sub
```

> **注意**
>
> 有关使用 `Find` 方法的更多信息，可以在本文档的“编辑标准对象、智能对象和 Windows 对象”标题下找到。

**另请参见**
*   Type 属性 (Page 2532)
*   Name 属性 (Page 2428)
*   Property 对象 (Page 2124)
*   HMIObjects 对象 (列表) (Page 2072)
*   如何编辑默认对象、智能对象、Windows 对象和管道对象 (Page 1752)
*   VBA 参考 (Page 1830)

##### FindByDisplayName 方法

**描述**
在整个组件库中搜索指定的对象。
搜索结果以 `FolderItem` 对象的形式返回。

> **注意**
>
> **搜索范围**
> 搜索在找到第一个对象时结束。
>
> **语言依赖性**
> 对象的显示名称是与语言相关的。
> 搜索时只考虑当前设置的语言。
>
> **图形编辑器：搜索依赖性**
> 如果在图形编辑器中选择了被搜索的对象或其上级文件夹，该方法可能无法访问该对象。
> 在这种情况下，搜索将不返回结果或返回错误消息。

**语法**
`Expression.FindByDisplayName(DisplayName)`

**Expression**
必需。一个返回 "SymbolLibrary" 类型对象或 "FolderItems" 列表的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `DisplayName` (String) | 要在块库中搜索的对象的显示名称。 |

**示例**
在以下示例中，将在整个库中搜索对象 "PC" 并输出其显示名称：

```vb
Sub FindObjectInSymbolLibrary()
'VBA154
Dim objGlobalLib As HMISymbolLibrary
Dim objFItem As HMIFolderItem
Set objGlobalLib = Application.SymbolLibraries(1)
Set objFItem = objGlobalLib.FindByDisplayName("PC")
MsgBox objFItem.DisplayName
End Sub
```

**另请参见**
*   FolderItem 对象 (Page 2055)
*   使用 VBA 访问组件库 (Page 1728)

##### FireConnectionEvents 方法

**描述**
由图形编辑器内部使用。

##### FlipHorizontally 方法

**描述**
将指定画面中选定的对象沿水平中线镜像。
对象类型决定了是否允许镜像（例如，OLE 元素不能被镜像）。执行镜像时，属性会被适当地修改。例如，如果您将 "StaticText" 类型的对象沿水平中线镜像，"AlignmentTop" 属性的值将从 "0" 变为 "2"。

**语法**
`Expression.FlipHorizontally()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，将在活动画面中插入一个 `StaticText` 对象，并沿水平中线镜像：

```vb
Sub FlipObjectHorizontally()
'VBA155
Dim objStaticText As HMIStaticText
Dim strPropertyName As String
Dim iPropertyValue As Integer
Set objStaticText = ActiveDocument.HMIObjects.AddHMIObject("Textfield", "HMIStaticText")
strPropertyName = objStaticText.Properties("Text").Name
With objStaticText
.Width = 120
.Text = "示例文本"
.Selected = True
iPropertyValue = .AlignmentTop
MsgBox "翻转前 '" & strPropertyName & "' 的值: " & iPropertyValue
ActiveDocument.Selection.FlipHorizontally
iPropertyValue = objStaticText.AlignmentTop
MsgBox "翻转后 '" & strPropertyName & "' 的值: " & iPropertyValue
End With
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### FlipVertically 方法

**描述**
将指定画面中选定的对象沿垂直中线镜像。
对象类型决定了是否允许镜像（例如，OLE 元素不能被镜像）。执行镜像时，属性会被适当地修改。例如，如果您将 "StaticText" 类型的对象沿垂直中线镜像，"AlignmentLeft" 属性的值将从 "0" 变为 "2"。

**语法**
`Expression.FlipVertically()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，将在活动画面中插入一个 `StaticText` 对象，并沿垂直中线镜像：

```vb
Sub FlipObjectVertically()
'VBA156
Dim objStaticText As HMIStaticText
Dim strPropertyName As String
Dim iPropertyValue As Integer
Set objStaticText = ActiveDocument.HMIObjects.AddHMIObject("Textfield", "HMIStaticText")
strPropertyName = objStaticText.Properties("Text").Name
With objStaticText
.Width = 120
.Text = "示例文本"
.Selected = True
.AlignmentLeft = 0
iPropertyValue = .AlignmentLeft
MsgBox "翻转前 '" & strPropertyName & "' 的值: " & iPropertyValue
ActiveDocument.Selection.FlipVertically
iPropertyValue = objStaticText.AlignmentLeft
MsgBox "翻转后 '" & strPropertyName & "' 的值: " & iPropertyValue
End With
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### ForwardOneLevel 方法

**描述**
将选定的对象在其当前图层内向前移动一个层级。

**语法**
`Expression.ForwardOneLevel()`

**Expression**
必需。一个返回 "Selection" 类型对象的表达式或元素。

**参数**
--

**示例:**
在以下示例中，在活动画面中插入了两个对象。第一个插入的对象然后被向前移动一个层级：

```vb
Sub MoveObjectOneLevelForward()
'VBA174
Dim objCircle As HMICircle
Dim objRectangle As HMIRectangle
Set objCircle = ActiveDocument.HMIObjects.AddHMIObject("sCircle", "HMICircle")
Set objRectangle = ActiveDocument.HMIObjects.AddHMIObject("sRectangle", "HMIRectangle")
With objCircle
.Top = 40
.Left = 40
.Selected = True
End With
With objRectangle
.Top = 40
.Left = 40
.Width = 100
.Height = 50
.BackColor = RGB(255, 0, 255)
.Selected = False
End With
MsgBox "对象已创建并选择！"
ActiveDocument.Selection.ForwardOneLevel
End Sub
```

**另请参见**
*   SelectedObjects 对象 (列表) (Page 2141)
*   VBA 参考 (Page 1830)

##### GetDeclutterObjectSize 方法

**描述**
读取指定画面中显示和隐藏对象（清理）的限制。

**语法**
`Expression.GetDeclutterObjectSize(Min, Max)`

**Expression**
必需。一个返回 "Document" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Min` (Long) | 以像素为单位的较小尺寸范围。 |
| `Max` (Long) | 以像素为单位的较大尺寸范围。 |

**示例**
在以下示例中，读取并输出活动画面的清理限制：

```vb
Sub ReadSettingsOfPicture()
'VBA848
Dim objectsize_min As Long, objectsize_max As Long
 
ActiveDocument.GetDeclutterObjectSize objectsize_min, objectsize_max
MsgBox objectsize_min & " " & objectsize_max
 
End Sub
```

##### GetItemByPath 方法

**描述**
返回位于组件库中指定内部访问路径上的一个 `FolderItem` 对象（文件夹或对象）。

> **注意**
>
> 要获取内部访问路径，请从弹出菜单中选择 "复制路径" 命令。文件夹或对象的内部访问路径将被复制到剪贴板。

**语法**
`Expression.GetItemByPath(PathName)`

**Expression**
必需。一个返回 "SymbolLibrary" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `PathName` (String) | 对象在组件库中的内部访问路径。 |

**示例:**
在此示例中，将返回整个库中的一个对象并输出其显示名称：

```vb
Sub ShowDisplayName()
'VBA157
Dim objGlobalLib As HMISymbolLibrary
Dim objFItem As HMIFolderItem
Set objGlobalLib = Application.SymbolLibraries(1)
Set objFItem = objGlobalLib.GetItemByPath("\Folder1\Folder2\Object1")
MsgBox objFItem.DisplayName
End Sub
```

**另请参见**
*   SymbolLibrary 对象 (Page 2162)
*   FolderItem 对象 (Page 2055)
*   使用 VBA 访问组件库 (Page 1728)

##### InsertFromMenuItem 方法

**描述**
在现有的用户定义工具栏中插入一个新图标，该图标引用用户定义菜单中现有的菜单项。
如果您希望设置一个工具栏，使其包含与现有用户定义菜单相同的命令，请使用此方法。

**语法**
`Expression.InsertFromMenuItem(Position, Key, pMenuItem, DefaultToolTipText)`

**Expression**
必需。一个返回 "ToolbarItems" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Position` (Long) | 定义图标在用户定义工具栏中的位置。 |
| `Key` (Variant) | 标识该符号。为 "Key" 使用唯一的名称（例如 tItem1_1）。 |
| `pMenuItem` (HMIMenuItem) | 打算引用的 `MenuItem` 对象。 |
| `DefaultToolTipText` (String) | 为相关图标定义当您将鼠标悬停在图标上时将显示的工具提示文本。 |

**示例:**
在此示例中，将在活动画面中插入一个用户定义的菜单和一个用户定义的工具栏。该图标调用用户定义菜单中的 "Hello World" 菜单项：

```vb
Sub ToolbarItem_InsertFromMenuItem()
'VBA158
Dim objMenu As HMIMenu
Dim objToolbarItem As HMIToolbarItem
Dim objToolbar As HMIToolbar
Dim objMenuItem As HMIMenuItem
Set objMenu = Application.CustomMenus.InsertMenu(1, "Menu1", "TestMenu")
'
'*************************************************
'* 注意：
'* 对象引用必须是唯一的。
'*************************************************
'
Set objMenuItem = Application.CustomMenus(1).MenuItems.InsertMenuItem(1, "MenuItem1", "Hello World")
Application.CustomMenus(1).MenuItems(1).Macro = "HelloWorld"
Set objToolbar = Application.CustomToolbars.Add("Toolbar1")
Set objToolbarItem = Application.CustomToolbars(1).ToolbarItems.InsertFromMenuItem(1, "ToolbarItem1", objMenuItem, "调用 TestMenu 的 Hello World")
End Sub
 
Sub HelloWorld()
MsgBox "过程 'HelloWorld()' 已执行。"
End Sub
```

**另请参见**
*   ToolbarItems 对象 (列表) (Page 2172)
*   InsertSeparator 方法 (Page 1937)
*   Add 方法 (CustomToolbars 列表) (Page 1873)
*   VBA 参考 (Page 1830)
*   创建自定义菜单和工具栏 (Page 1709)

##### InsertMenu 方法

**描述**
创建一个新的、用户定义的菜单。应用程序级和画面级的用户定义菜单有所不同：
*   **应用程序级菜单**: 链接到图形编辑器，并且在图形编辑器中的所有画面都关闭时也可见。将 VBA 代码放在名为 "GlobalTemplateDocument" 或 "ProjectTemplateDocument" 的文档中，并使用 `Application` 属性。
*   **画面级菜单**: 与特定画面链接，并且只要该画面可见就保持可见。将 VBA 代码放在所需画面的 "ThisDocument" 文档中，并使用 `ActiveDocument` 属性。

**语法**
`Expression.InsertMenu(Position, Key, DefaultLabel)`

**Expression**
必需。一个返回 "CustomMenus" 类型对象的表达式或元素。

**参数**

| 参数 (数据类型) | 描述 |
| :--- | :--- |
| `Position` (Long) | 定义用户定义菜单在菜单栏中的位置。但是，画面级菜单总是位于应用程序级菜单的右侧。 |
| `Key` (Variant) | 标识用户定义的菜单。为 "Key" 使用唯一的名称（例如 "DocMenu1"）。 |
| `DefaultLabel` (String) | 用户定义菜单的名称。 |

**示例:**
在以下示例中，将在活动画面中创建一个带有两个菜单项和一个带有两个条目的子菜单的用户定义菜单。子菜单将通过一条分割线在视觉上区分开来：

```vb
Sub CreateDocumentMenus()
'VBA159
Dim objDocMenu As HMIMenu
Dim objMenuItem As HMIMenuItem
Dim objSubMenu As HMIMenuItem
'
Set objDocMenu = ActiveDocument.CustomMenus.InsertMenu(1, "DocMenu1", "Doc_Menu_1")
